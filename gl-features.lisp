#|
 This file is a part of trial
 (c) 2016 Shirakumo http://tymoon.eu (shinmera@tymoon.eu)
 Author: Nicolas Hafner <shinmera@tymoon.eu>
|#

(in-package #:org.shirakumo.fraf.trial)

(defvar *gl-features* #(:blend
                        :clip-distance0
                        :clip-distance1
                        :color-logic-op
                        :cull-face
                        :debug-output
                        :debug-output-synchronous
                        :depth-clamp
                        :depth-test
                        :dither
                        :framebuffer-srgb
                        :line-smooth
                        :multisample
                        :polygon-offset-fill
                        :polygon-offset-line
                        :polygon-offset-point
                        :polygon-smooth
                        :primitive-restart
                        :primitive-restart-fixed-index
                        :rasterizer-discard
                        :sample-alpha-to-coverage
                        :sample-alpha-to-one
                        :sample-coverage
                        :sample-shading
                        :sample-mask
                        :scissor-test
                        :stencil-test
                        :texture-cube-map-seamless
                        :program-point-size))

(defvar *default-enabled-gl-features* #(:dither :multisample))

(defun make-feature-table (&optional parent)
  (let ((table (make-hash-table :test 'eq :size (length *gl-features*))))
    (if parent
        (loop for k being the hash-keys of parent
              for v being the hash-values of parent
              do (setf (gethash k table) v))
        (reset-features table))
    table))

(defun reset-features (&optional (table (feature-table)))
  (loop for k across *gl-features*
        do (if (find k *default-enabled-gl-features*)
               (setf (gethash k table) T)
               (setf (gethash k table) NIL))))

(defvar *feature-stack* (list (make-feature-table)))

(defun feature-table ()
  (first *feature-stack*))

(defun enable-feature (&rest features)
  (let ((table (feature-table)))
    (dolist (feature features)
      (unless (gethash feature table)
        (gl:enable feature)
        (setf (gethash feature table) T)))))

(define-compiler-macro enable-feature (&whole whole &environment env &rest features)
  (let ((constants) (variants) (table (gensym "TABLE")))
    (dolist (feature features)
      (if (constantp feature env)
          (push feature constants)
          (push feature variants)))
    (cond ((and (null constants) (null variants))
           ())
          ((null constants)
           whole)
          (T
           `(let ((,table (feature-table)))
              (enable-feature ,@variants)
              ,@(loop for constant in constants
                      for feature = `(load-time-value ,constant)
                      collect `(unless (gethash ,feature ,table)
                                 (gl:enable ,feature)
                                 (setf (gethash ,feature ,table) T))))))))

(defun disable-feature (&rest features)
  (let ((table (feature-table)))
    (dolist (feature features)
      (when (gethash feature table)
        (gl:disable feature)
        (setf (gethash feature table) NIL)))))

(define-compiler-macro disable-feature (&whole whole &environment env &rest features)
  (let ((constants) (variants) (table (gensym "TABLE")))
    (dolist (feature features)
      (if (constantp feature env)
          (push feature constants)
          (push feature variants)))
    (cond ((and (null constants) (null variants))
           ())
          ((null constants)
           whole)
          (T
           `(let ((,table (feature-table)))
              (disable-feature ,@variants)
              ,@(loop for constant in constants
                      for feature = `(load-time-value ,constant)
                      collect `(when (gethash ,feature ,table)
                                 (gl:disable ,feature)
                                 (setf (gethash ,feature ,table) NIL))))))))

(defun push-features (&optional (table (make-feature-table (feature-table))))
  (push table *feature-stack*))

(defun pop-features ()
  (let ((prev (pop *feature-stack*))
        (cur (feature-table)))
    (loop for k being the hash-keys of prev
          for v being the hash-values of prev
          do (cond ((and v (not (gethash k cur)))
                    (gl:disable k))
                   ((and (not v) (gethash k cur))
                    (gl:enable k))))))

(defmacro with-pushed-features (&body body)
  `(progn (push-features)
          (unwind-protect
               (progn ,@body)
            (pop-features))))

(defclass gl-context (context)
  ((glsl-target-version :initarg :glsl-version :initform NIL :accessor glsl-target-version))
  (:default-initargs
   :version '(3 3)
   :profile :core))

(defmethod create-context :after ((context glcontext))
  (cache-gl-extensions))

(defmethod glsl-target-version ((context gl-context))
  (let ((slot (slot-value context 'glsl-target-version)))
    (or slot (format NIL "濑濑阿鲥蝮轱泔铘屮舂┅┅ㄤ彐礤翳镤珈箪鲥蝮轱瞽桢徜弪è泔铘屮珈泔铘屮舂ㄦ矧磲紊ｖ弪箝镱狺累狺茛ㄧ祗飙翎蜱弭鲥蝮轱泔铘屮舂ㄣ狍痱镦殪泔铘屮舂ê泔蝈泔蝈ê弩㈠螈┅┅ㄤ彐礤翳镤珈箪翎蜱弭鲥蝮轱è溴驷蹯ㄥ耢冤┅ㄩ泔铘屮舄ㄧ祗飙翎蜱弭鲥蝮轱泔铘屮舄⒊嘲┅ㄤ彐磲泸鏖翳蝈钿弪箦趑轭珞箦趑轭珞怙澌怙澌戾è翳躅ㄧ孱簌⒃日嗡┅箦趑轭珞祜镳骘箦趑轭轭箦趑轭珞泔祆邈ㄥ豉疱汜箦箦趑轭脲黠蜾ㄥ汜箦箦趑轭ê铒溴痿璀黩轸弩啜黩轸瀛麸溴痿璀紊泰ê徜溟糸鲥忪孱啜忪孱洵盹溴横滗轸轹紊泰ê骝镱舡沲祆啜沲祆驷沐烘蝻铘衡徙氅换领蝈徜溴驷蹯趔换粕赝藕磲脲铄篝邃鏖翳蝈钿弪徙趱犰禊滹翳蜷玷翳轭ê溴痿璀黩轸弩啜黩轸瀛麸溴痿璀冤ê箫躜沐忪孱啜忪孱洵盹溴紊紊泰ê忉汶沲祆啜沲祆驷沐衡徙衡徙氅┅ㄣ镱┅┅啜骒弭è翳躅ī棱镤┅括祜镳骘ㄦ躅镱镦姗轭箦趑轭珞躅戾篌ㄥ耢镱н泔祆邈啜箦翩ì骢钽泔铘屮舄镱┅眭祠轲戾鲠祯瀛痱镧ì翳躅氅括祜镳骘ㄦ躅镱镦姗轭箦趑轭珞躅戾篌ㄥ耢镦н泔祆邈啜箦翩ì骢钽泔铘屮舄镦姗┅┅ㄤ彐礤翳镤箦翩黩轸瀛麸溴痿瑭磲箅ㄣ镱翦珈泔铘屮舂ㄧ旌溴痿璀磲箅磲箅┅ㄤ彐礤翳镤箦翩溴痿璀盹溴盹溴ㄣ镱翦珈泔铘屮舂ㄥ汜箦盹溴è紊泰ㄧ旌溴痿璀骢钽侯弼弪┅è冤ㄧ旌溴痿璀骢钽横祺狴螬ㄧ旌溴痿璀骢钽哄聃犰┅ǒㄧ旌溴痿璀骢钽侯雉羼踽飑冀ㄧ旌溴痿璀骢钽红羼踽飑窘ㄧ旌溴痿璀骢钽红羼踽飑ㄧ旌溴痿璀骢钽红弩螬ㄧ旌溴痿璀骢钽虹蝈狒弪┅┅ㄤ彐礤翳镤箦翩忪孱洵盹溴盹溴ㄣ镱翦珈泔铘屮舂ㄥ汜箦盹溴ê徜溟糸鲥ㄧ旌忪孱洵骢钽后蜚犰痂猴铄┅è紊轰彐狨祠后秕蜚瀛秭弪ㄧ旌忪孱洵骢钽箦疳蜥翦后蜚犰痂猴铄黹铛蟓篁悱犰痂猴铄猴铄黹铛蟓篁悱犰痂岍┅ㄤ彐礤翳镤箦翩沲祆轭绛盹溴盹溴ㄣ镱翦珈泔铘屮舂ㄥ汜箦盹溴è紊泰ㄤ轶徕戾驽狒躜恒蹯飙驷沐┅è冤ㄥ钺忪瀛驽狒躜恒蹯飙驷沐┅è轰彐狨祠衡徙氕驷沐螬ㄧ旌沲祆驷沐衡徙氅ê骝镱舡驷沐ㄧ旌沲祆驷沐烘蝻铘┅┅ㄤ彐礤翳镤箦翩篝孱汩飙盹溴盹溴ㄣ镱翦珈泔铘屮舂ㄥ汜箦盹溴è紊泰ㄧ旌篝孱汩飙骢钽侯弼弪辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓è冤ㄧ旌篝孱汩飙骢钽横祺狴辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓ㄧ旌篝孱汩飙骢钽哄聃犰辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓ǒㄧ旌篝孱汩飙骢钽侯雉羼踽辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓è冀轰彐狨祠ㄧ旌篝孱汩飙骢钽红羼踽辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓窘ㄧ旌篝孱汩飙骢钽虹羼踽辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓ㄧ旌篝孱汩飙骢钽虹蝈狒弪辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓ㄧ旌篝孱汩飙骢钽红弩辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋弘邋皓ūㄧ旌篝孱汩飙骢钽横祺狴辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋洪钽颟ūㄧ旌篝孱汩飙骢钽横祺狴辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋轰邈颟ㄣ戾狎ㄧ旌篝孱汩飙骢钽横祺狴辈ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋候屦灬沐┅ㄔㄧ旌篝孱汩飙骢钽横祺狴盹溴ｘ破破破ㄧ旌篝孱汩飙镳弘邋弘邋候屦灬沐┅┅ㄤ彐礤翳镤箦翩沆遽颦泔祜颟è鲥鲥愠ㄣ镱翦珈泔铘屮舂ㄧ旌沆遽颦泔祜鲽鲥悌鳄鲥悌鳅鲥悌碑癌ㄤ彐礤翳镤箦翩沆遽颦泔祜颟è鲥鲥愦ㄣ镱翦珈泔铘屮舂ㄧ旌沆遽颦泔祜鲽鲥悌鳄鲥悌鳅鲥悌鲼鲥悌┅ㄤ彐礤翳镤箦翩沆遽颦泔祜颟è轭轭翦珏颟ㄣ镱翦珈泔铘屮舂戾è熹ㄢ翦癌轭舂ㄧ熹ㄢ翦俯轭舂ㄢ熹ㄢ翦倍轭舂ㄡ熹ㄢ翦泊轭舂┅ㄧ旌沆遽颦泔祜ǒ驳诞癌ǒ驳诞癌ǒ驳诞癌ǒ驳诞癌┅换梏麴蠛鼢鳟腓蝻铒螽矧绡蝈玳篝蝙橡孱翘屮翦铙轱铙猎莎猎蛇礤黹铈锂豇ㄤ彐躅ョ飙琊醐蝻镯狒ī戾舄è鲡锃骝邋礤盹蝙狒ㄧ旌珏舡轭翦珏ｘ阜坡穿翦骝邋礤盹蝙狒ㄧ旌珏舡轭翦珏ｘ阜泼穿ㄢ蹑骝邋礤盹蝙狒ㄧ旌珏舡轭翦珏ｘ阜颇穿麸翎ǐㄡ蝈鲡锃骝邋礤盹蝙狒癌ㄡ蝈翦骝邋礤盹蝙狒癌ㄡ蝈怩姝骝邋礤盹蝙狒癌┅鲠祯弩麸翎麸翎飑┅换梏麴函溴鲥祜疱虍滹黝祜徜铞殇獒泔懑镳孱珈箴邈蟑翘呶重哏瘐唔屙矧唛铈锂豇ㄤ彐躅ョ飙琊醐蝻镯铞殇獒ī戾è鲩漤屙麸翎ㄧ旌珏舡轭翦珏ｘ拱捶暴鲩漤屙骝邋ㄧ旌珏舡轭翦珏ｘ拱垂暴┅鲠祯弩鲩漤屙骝邋鲩漤屙麸翎飑┅ㄤ彐礤翳镤琊醐蝻镯è泔铘屮珈泔铘屮舂磲泸镬弭è觊翳轭绌啜殓铒蝈弪蝻蝮蝈趱蝾骝镯琊醐蝻镯眭祠轲戾鲠祯瀛痱镧翳轭ㄣ镯痖戾х瘐蝻镯灬礅溽ī翳轭绌┅┅┅觊ē珈琊醐蝻镯狒椹觊ē珈琊醐蝻镯铞殇獒┅觊鲠祯弩暴┅ㄤ彐礤翳镤磲翦趱蝈殇è泔铘屮珈泔铘屮舂ㄧ旌珏舡轭翦珏喉狲翦趱蝈轫徵瀛躅轸螬ㄤ彐轭瀛珈镡犰珈屮翦铙轱铙īㄤ彐躅汜汨瀛珈屮翦铙轱铙ī戾è疳汶徵濯ㄦ轭洵疳汶徵⑺刨紫夷┅箦翩珈屮翦铙轱铙祜镳骘骝镯忮祜ㄧ旌珏舄侯蹴屮翦铙轱铙骘钺礤ㄩ珙矧瀛弪蝻蝮ㄧ旌珏舡篝蜷铉哄翦铙轱铙椹麒孱钺礤泔祆邈ㄣ骀楹趄犷箪狒瀛钺礤骝镯骘蝈殓钺礤疳汶徵濯┅┅ㄤ彐躅珈屮翦铙轱瞽ㄥ翦铙轱瞟ㄦ轭屮翦铙轱珈屮翦铙轱铙┅ㄤ彐躅扉篝珈屮翦铙轱铙ī矧珈屮翦铙轱铙ㄣ徙桢珈屮翦铙轱铙┅ㄤ彐磲泸麒孱珈屮翦铙轱ㄥ翦铙轱怙澌怙澌戾è扉篝ㄥ铎轶屮翦铙轱瞟┅换韵南橡糸黹箦翳轶怡汜汨轭翳翦篝徭翦骈蝮蝓铘轫瀹啜麒孱ㄡ钿括祜镳骘屮翦铙轱轭扉篝泔祆邈啜骈钿屮翦铙轱珈屮翦铙轱铙┅棱镤┅ㄤ彐磲泸珈屮翦铙轱瞽汜箦é怙澌汜箦螬啜泔钿括祜镳骘ㄥ翦铙轱铙怙澌轭汜箦泔祆邈ㄣ狍屮翦铙轱铙è雉桢蝼轶濠啜棱镤┅ㄔ啜ㄡ钿括祜镳骘屮翦铙轱轭ㄥ铎轶屮翦铙轱铙泔祆邈啜骈钿屮翦铙轱珈屮翦铙轱铙┅棱镤┅┅┅ㄤ彐躅珈痱镳弪豉钺礤ㄨ犷潇弪汜箦ㄧ旌珏舄钺礤ㄥ蝌矧ㄥ蝌ㄤ邈灬蝈ㄩ珙矧弪颟乎钺鲠殪徕戾┅ㄤ彐躅珈鲥钿矧ī戾è鲥钿矧ㄧ旌珏舡篝蜷铉忽孱滹颟┅ㄣ镱è箦狎汨⑸铘屐鲥钿矧洪铘屐è箦狎汨⑽稚纳立鲥钿矧侯鲩溟岍è箦狎汨⒘陨鲥钿矧横礓è箦狎汨⒘湍鲥钿矧横礓ㄔ乎铍铒黝┅┅ㄤ彐礤翳镤泔铘屮舡轭骘è泔铘屮珈泔铘屮舂脲篝蝈犴篝犷溽蜾秕麴豸箬秣屮翦铙轱铙冤ㄦ矧磲篝蝈犴ヒ躅铋铉翘岙狺俞眇戾怩骀弪蠛筢眇戾吼歪翦趱蝈箝搴狺歪翦趱蝈躅轸蠛狺累歪泔眇豸珧秕痼狺歪黠螂珧秕痼岍]                    GL Vendor:          a%                    GL Renderer:        a%                    GL Version:         a%                    GL Shader Language: a%                    @[GL Extensions:      狺%]"
          (gl-property :major-version)
          (gl-property :minor-version)
          (profile gl-context)
          (gl-property :sample-buffers)
          (gl-property :samples)
          (gl-property :max-texture-size)
          (gl-property :max-vertex-texture-image-units)
          ;; Fuck you, GL, and your stupid legacy crap.
          (gl-property :max-texture-image-units)
          (gl-property :max-tess-control-texture-image-units)
          (gl-property :max-tess-evaluation-texture-image-units)
          (gl-property :max-geometry-texture-image-units)
          (gl-property :max-compute-texture-image-units)
          (when-gl-extension :GL-ARB-COMPUTE-SHADER
                             (append (coerce (gl-property :max-compute-work-group-count) 'list)
                                     (coerce (gl-property :max-compute-work-group-size) 'list)
                                     (list (gl-property :max-compute-work-group-invocations))))
          (gl-property :vendor)
          (gl-property :renderer)
          (gl-property :version)
          (gl-property :shading-language-version)
          (when show-extensions (list-gl-extensions))))
